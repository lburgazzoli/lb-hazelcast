// *****************************************************************************
//
// *****************************************************************************

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    }
}

// *****************************************************************************
//
// *****************************************************************************

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'osgi'
    apply plugin: 'nexus'

    version             = '1.0.0-SNAPSHOT'
    group               = 'com.github.lburgazzoli'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    ext {
        gradleVersion   = '2.0'
        gradleScriptDir = "${rootProject.projectDir}/gradle"

		if(!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
			if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
				nexusUsername = sonatypeUsername
				nexusPassword = sonatypePassword
			} else {
				nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
				nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
			}
		}

        prefix           = 'lb'
        isReleaseVersion = !version.endsWith("SNAPSHOT")
        gitRoot          = "https://github.com/lburgazzoli"
        gitProject       = "https://github.com/lburgazzoli/lb-hazelcast"

        versions = [
            slf4j             : '1.7.5',
            logback           : '1.1.2',
            jackson           : '2.4.1',
            jacksonSmile      : '2.4.1',
            hazelcast         : '3.2.3',
            guava             : '17.0',
            commonsLang       : '3.3.2',
            commonsCollections: '4.0',
            commonsPool       : '2.2',
            commonsIO         : '2.4',
            spring            : '4.0.6.RELEASE',
            springDataJpa     : '1.6.1.RELEASE',
            springReactor     : '1.1.3.RELEASE',
            springIntegration : '4.0.3.RELEASE',
            junit             : '4.11',
            kryo              : '2.23.0',
            disruptor         : '3.2.1',
            gsCollections     : '5.0.0',
            groovy            : '2.3.4',
            openhftLang       : '6.4.4',
            openhftChronicle  : '3.1.1',
            openhftCollection : '3.1.0',
            openhftAffinity   : '2.1.0',
            openhftCompiler   : '2.2.0',
            jsonpath          : '1.2'
        ]
    }

    // *************************************************************************
    //
    // *************************************************************************

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    // *************************************************************************
    //
    // *************************************************************************


    repositories {
        mavenLocal()
        mavenCentral()
		
        maven { url 'http://repo.springsource.org/release' }
        maven { url 'https://oss.sonatype.org/content/repositories/releases' }
		maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
	
    dependencies {
        compile "org.slf4j:slf4j-api:$versions.slf4j"        

        compile "com.google.guava:guava:$versions.guava"
        compile "commons-io:commons-io:$versions.commonsIO"
        compile "org.apache.commons:commons-pool2:$versions.commonsPool"
        compile "org.apache.commons:commons-lang3:$versions.commonsLang"
        compile "org.apache.commons:commons-collections4:$versions.commonsCollections"
        compile "com.goldmansachs:gs-collections:$versions.gsCollections"

        compile "org.codehaus.groovy:groovy:$versions.groovy"
        compile "com.hazelcast:hazelcast-all:$versions.hazelcast"

        testCompile "junit:junit:$versions.junit"
		
		runtime "org.slf4j:slf4j-ext:$versions.slf4j"
        runtime "org.slf4j:jul-to-slf4j:$versions.slf4j"
        runtime "org.slf4j:jcl-over-slf4j:$versions.slf4j"
        runtime "ch.qos.logback:logback-core:$versions.logback"
		runtime "ch.qos.logback:logback-classic:$versions.logback"
    }

    test {
        testLogging {
            events          'failed'
            exceptionFormat 'full'
        }
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = [
        "-Xlint:unchecked",
        "-Xlint:deprecation",
    ]

    // *************************************************************************
    //
    // *************************************************************************

    jar {
        baseName = "${project.prefix}-${project.name}"

        manifest.attributes['Bundle-Description'     ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-Name'            ] = "${project.prefix} :: ${project.name}"
        manifest.attributes['Bundle-SymbolicName'    ] = "${project.group}-${project.prefix}-${project.name}"
        manifest.attributes['Bundle-Version'         ] = project.version
        manifest.attributes['Implementation-Title'   ] = "${project.group}-${project.prefix}-${project.name}-${project.version}"
        manifest.attributes['Implementation-Version' ] = project.version
        manifest.attributes['Implementation-Vendor'  ] = 'lb'

        manifest.classesDir = new File("$buildDir/classes/main")
        manifest.classpath = project.runtimeClasspath
    }

    // *************************************************************************
    //
    // *************************************************************************

    nexus {
        attachJavadoc         = true
        attachSources         = true
        attachTests           = false
        sign                  = false
    }

    modifyPom {
        project {
			name          = "${project.prefix}-${project.name}"
			description   = "${project.prefix}-${project.name}"
			url           = "${project.gitProject}"
			packaging     = 'jar'

			licenses {
				license {
					name         = "The Apache Software License, Version 2.0"
					url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
					distribution = "repo"
				}
			}

			scm {
				url                 = "${project.gitProject}"
				connection          = "scm:git:${project.gitProject}.git"
				developerConnection = "scm:git:${project.gitProject}.git"
			}
		}
	}
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}
